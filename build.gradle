group 'com.github.tix-measurements'
version '2.2.2-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
	}
}

repositories {
    mavenCentral()
}

ext {
    nettyVersion = "4.0.36.Final"
    commonsLangVersion = "3.4"
    guavaVersion = "19.0"
    assertjVersion = "3.4.1"
    log4jVersion = "2.5"
    junitVersion = "4.12"
    mockitoVersion = "1.9.5"
}

dependencies {
    // Core
    compile group: 'io.netty', name: 'netty-all', version: "${nettyVersion}"
    // Libs & Misc.
    compile group: 'org.apache.commons', name: 'commons-lang3', version: "${commonsLangVersion}"
    compile group: 'com.google.guava', name: 'guava', version: "${guavaVersion}"
    compile group: 'org.assertj', name: 'assertj-core', version: "${assertjVersion}"
    // Logging
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"
    // Testing
    testCompile group: 'junit', name: 'junit', version: "${junitVersion}"
    testCompile group: 'org.mockito', name: 'mockito-all', version: "${mockitoVersion}"
}

task getVersion {
    println version
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/test-results/coverage.xml")
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

modifyPom {
    project {
        name 'tix-time-core'
        description 'TiX Time Core Lib for Netty Server'
        url 'https://github.com/TiX-measurements/tix-time-core'
        packaging 'jar'
        organization {
            name 'TiX'
            url 'http://tix.innova-red.net'
        }
        licenses {
            license {
                name 'GNU Lesser General Public License V3'
                url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/TiX-measurements/tix-time-core'
            connection 'scm:git:https://github.com/TiX-measurements/tix-time-core'
            developerConnection 'scm:git@github.com:TiX-measurements/tix-time-core.git'
        }
        developers {
            developer {
                id 'fnmartinez'
                name 'Facundo Martinez Correa'
            }
            developer {
                id 'mannias'
                name 'Mat√≠as Domingues'
            }
            developer {
                id 'jperezcu'
                name 'Javier Perez Cunarro'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}
